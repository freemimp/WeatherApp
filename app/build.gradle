plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'com.google.devtools.ksp'
}

android {
    compileSdk compile_sdk

    defaultConfig {
        applicationId "uk.co.freemimp.weatherapp"
        minSdk min_sdk
        targetSdk target_sdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "uk.co.freemimp.weatherapp.CustomTestRunner"

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'

        buildConfigField "String", "BASE_URL", project.findProperty("base_url")
        buildConfigField "String", "API_KEY", project.findProperty("api_key")
    }

    buildFeatures {
        viewBinding true
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.md', 'META-INF/LICENSE-notice.md']
        }
    }

    namespace 'uk.co.freemimp.weatherapp'
}

dependencies {
    implementation project(":main")
    implementation project(":map")
    implementation project(":data")
    implementation project(":core")
    implementation project(":domain")
    implementation project(":common_test")

    //Android
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "com.google.android.gms:play-services-maps:$google_maps_version"
    implementation "com.google.android.material:material:$android_materia_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.fragment:fragment:$fragment_version"

    //Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Testing
    androidTestImplementation "androidx.test.ext:junit:$test_ext_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core_version"
    androidTestImplementation "io.mockk:mockk-android:$mockk_version"
    androidTestImplementation "androidx.test:runner:$test_core_runner_version"
    androidTestUtil "androidx.test:orchestrator:$test_orchestrator_version"
    androidTestImplementation "androidx.test:core:1.5.0"
    androidTestImplementation "androidx.test:core-ktx:1.5.0"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"
    androidTestAnnotationProcessor "com.google.dagger:hilt-android-compiler:$hilt_version"
    androidTestImplementation ("com.adevinta.android:barista:$barista_version") {
        exclude group: 'org.jetbrains.kotlin' // Only if you already use Kotlin in your project
    }
}
